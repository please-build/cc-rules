"""Rules to build C and C++ targets.

Note that the C / C++ build process is complex with many options; we attempt to keep things
as high-level as possible here but expose flags to tune things as needed.

As a general note, most of the rules work by attaching labels indicating various flags etc which
later rules need to know about. These get picked up by later rules to adjust commands; this way
you can write a cc_library rule specifying e.g. linker_flags = ['-lz'] and not have to re-specify
that on every single cc_binary / cc_test that transitively depends on that library.
"""

# These options cause C/C++ compiler toolchains to terminate early (i.e., before they output an executable file). GCC
# doesn't give them a specific name; Clang calls them "actions". Not all of them are listed here, only the ones we're
# likely to add as part of these build definitions.
# GCC documentation: https://gcc.gnu.org/onlinedocs/gcc/Overall-Options.html
# Clang documentation: https://clang.llvm.org/docs/ClangCommandLineReference.html#actions
_ACTION_FLAGS = [
    "-E",           # GCC & Clang
    "--preprocess", # Long form of -E; Clang only
    "-S",           # GCC & Clang
    "--assemble",   # Long form of -S; Clang only
    "-c",           # GCC & Clang
    "--compile",    # Long form of -c; Clang only
    "--precompile", # Clang only
]

_COVERAGE_FLAGS = ["--coverage", "-fprofile-dir=."]

# Clang >= 16 just require C++20 features to be enabled in order to enable modules, while older versions explicitly
# require the -fmodules-ts flag.
_MODULE_FLAGS = ["""'{{ clang && clang < 16 ? "-fmodules-ts" : "-std=c++20" }}'"""]


def cc_library(name:str, srcs:list=[], hdrs:list=[], private_hdrs:list=[], deps:list=[], out:str='', optional_outs:list=[],
               visibility:list=None, test_only:bool&testonly=False, compiler_flags:list&cflags&copts=[],
               linker_flags:list&ldflags&linkopts=[], pkg_config_libs:list=[], pkg_config_cflags:list=[], includes:list=[],
               defines:list|dict=[], alwayslink:bool=False, linkstatic:bool=False, _c=False,
               textual_hdrs:list=[], _module:bool=False, _interfaces:list=[], labels:list=[]):
    """Generate a C++ library target.

    Args:
      name (str): Name of the rule
      srcs (list): C++ source files to compile.
      hdrs (list): Header files. These will be made available to dependent rules, so the distinction
                   between srcs and hdrs is important.
      private_hdrs (list): Header files that are available only to this rule and not exported to
                           dependent rules.
      deps (list): Dependent rules.
      out (str): Name of the output library. Defaults to lib<name>.a (or just <name>.a if name already
                       begins with 'lib').
      optional_outs (list): Name of optional outputs.
      visibility (list): Visibility declaration for this rule.
      test_only (bool): If True, is only available to other test rules.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker; these will not be used here but will be
                           picked up by a cc_binary or cc_test rule.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      includes (list): List of include directories to be added to the compiler's path.
      defines (list | dict): List of tokens to define in the preprocessor.
                             Alternatively can be a dict of name -> value to define, in which case
                             values are surrounded by quotes.
      alwayslink (bool): If True, any binaries / tests using this library will link in all symbols,
                         even if they don't directly reference them. This is useful for e.g. having
                         static members that register themselves at construction time.
      linkstatic (bool): Only provided for Bazel compatibility. Has no actual effect.
      textual_hdrs (list): Also provided for Bazel compatibility. Effectively works the same as hdrs for now.
      labels (list): Labels to attach to this rule.
    """
    # Bazel suggests passing nonexported header files in 'srcs'. We however treat
    # srcs as things to actually compile and must mark a distinction.
    if CONFIG.BAZEL_COMPATIBILITY:
        src_hdrs = [src for src in srcs if src.endswith('.h') or src.endswith('.inc')]
        srcs = [src for src in srcs if src not in src_hdrs]
        # This is rather nasty; people seem to be relying on being able to reuse
        # headers that they've put in srcs. We hence need to re-export them here, but really
        # they should be added to private_hdrs instead.
        hdrs += src_hdrs
        hdrs += textual_hdrs
        # Found this in a few cases... can't pass -pthread to the linker.
        linker_flags = ['-lpthread' if l == '-pthread' else l for l in linker_flags]

    # Handle defines being passed as a dict, as a nicety for the user.
    if isinstance(defines, dict):
        defines = [k if v is None else f'{k}=\\"{v}\\"' for k, v in sorted(defines.items())]

    pkg_name = package_name()
    labels = (['cc:ld:' + flag for flag in linker_flags] +
              ['cc:pc:' + lib for lib in pkg_config_libs] +
              ['cc:pcc:' + cflag for cflag in pkg_config_cflags] +
              ['cc:inc:' + join_path(pkg_name, include) for include in includes] +
              ['cc:def:' + define for define in defines] +
              labels)

    if not srcs and not _interfaces:
        # Header-only library, no compilation needed.
        return filegroup(
            name = name,
            srcs = hdrs,
            exported_deps = deps,
            labels = labels,
            test_only = test_only,
            visibility = visibility,
            output_is_complete = False,
        )

    # Collect the headers for other rules
    requires = ['cc_hdrs', 'cc_mod']
    hdrs_rule = filegroup(
        name = name,
        tag = 'hdrs',
        srcs = hdrs,
        requires = requires,
        deps = None if _module else deps,
        test_only = test_only,
        labels = labels,
        output_is_complete = False,
    )
    provides = {'cc_hdrs': hdrs_rule}

    # TODO(pebers): handle includes and defines in _library_cmds as well.
    pre_build = _library_transitive_labels(_c, compiler_flags, pkg_config_libs, pkg_config_cflags) if (deps or includes or defines or _interfaces) else None
    pkg = package_name()

    if _interfaces:
        # Generate the module interface file
        xflags = _MODULE_FLAGS + ["--precompile", "-x", "c++-module", "-o", '"$OUT"']
        cmds, tools = _library_cmds(_c, compiler_flags + xflags, pkg_config_libs, pkg_config_cflags, archive=False)
        interface_rule = build_rule(
            name = name,
            tag = 'interface',
            srcs = {'srcs': _interfaces, 'hdrs': hdrs, 'priv': private_hdrs},
            outs = [name + '.pcm'],
            cmd = cmds,
            building_description = 'Compiling...',
            requires = requires,
            test_only = test_only,
            labels = labels + [f'cc:mod:{pkg}/{name}.pcm'],
            tools = tools,
            needs_transitive_deps = True,
        )
        srcs += _interfaces
        all_deps = deps + [interface_rule]
        provides['cc_mod'] = interface_rule
    else:
        all_deps = deps

    cmds, tools = _library_cmds(_c, compiler_flags, pkg_config_libs, pkg_config_cflags)
    if not out:
        out = f'{name}.a' if name.startswith('lib') else f'lib{name}.a'
    if len(srcs) > 1:
        # Compile all the sources separately, this is much faster for large numbers of files
        # than giving them all to gcc in one invocation.
        a_rules = []
        for src in srcs:
            suffix = src.replace('/', '_').replace('.', '_').replace(':', '_').replace('|', '_')
            a_name = f'_{name}#{suffix}'
            a_rule = build_rule(
                name=a_name,
                srcs={'srcs': [src], 'hdrs': hdrs, 'priv': private_hdrs},
                outs=[a_name + '.a'],
                optional_outs=['*.gcno']+optional_outs,  # For coverage
                deps=deps if src in _interfaces else all_deps,
                cmd=cmds,
                building_description='Compiling...',
                requires=requires,
                test_only=test_only,
                labels=labels,
                tools=tools,
                pre_build=pre_build,
                needs_transitive_deps=True,
            )
            a_rules += [a_rule]

        # Combine the archives into one.
        a_rule = build_rule(
            name = name,
            tag = 'a',
            srcs = {'srcs': a_rules},
            outs = [out],
            cmd = '"$TOOLS_ARCAT" ar --combine && "$TOOLS_AR" s "$OUT"',
            building_description = 'Archiving...',
            test_only = test_only,
            labels = labels,
            output_is_complete = True,
            tools = {
                'arcat': [CONFIG.ARCAT_TOOL],
                'ar': [CONFIG.CC.AR_TOOL],
            },
        )
        if alwayslink:
            labels += [f'cc:al:{pkg}/{out}']

        # Filegroup to pick that up with extra deps. This is a little annoying but means that
        # things depending on this get the combined rule and not the individual ones, but do get
        # all the other dependencies which are probably important.
        lib_rule = filegroup(
            name = name,
            tag = 'lib',
            srcs = [a_rule],
            deps = deps,
            requires = ['cc_mod'] if _module else None,
            test_only = test_only,
            labels = labels,
            output_is_complete=False,
        )

    else:
        # Single source file, optimise slightly by not extracting & remerging the archive.
        cc_rule = build_rule(
            name=name,
            tag='cc',
            srcs={'srcs': srcs, 'hdrs': hdrs, 'priv': private_hdrs},
            optional_outs=['*.gcno']+optional_outs,  # For coverage
            deps=deps if srcs == _interfaces else all_deps,
            outs=[out],
            cmd=cmds,
            building_description='Compiling...',
            requires=requires,
            test_only=test_only,
            labels=labels,
            tools=tools,
            pre_build=pre_build,
            needs_transitive_deps=True,
        )
        if alwayslink:
            labels += [f'cc:al:{pkg}/{out}']
        # Need another rule to cover require / provide stuff. This is getting a bit complicated...
        lib_rule = filegroup(
            name = name,
            tag = 'lib',
            srcs = [cc_rule],
            deps = deps,
            requires = ['cc_mod'] if _module else None,
            test_only = test_only,
            labels = labels,
            output_is_complete=False,
        )

    provides['cc'] = lib_rule
    return filegroup(
        name=name,
        srcs=[lib_rule],
        deps=[hdrs_rule],
        provides=provides,
        test_only=test_only,
        visibility=visibility,
        output_is_complete=False,
    )


def cc_object(name:str, src:str, hdrs:list=[], private_hdrs:list=[], out:str=None, optional_outs:list=[], test_only:bool&testonly=False,
              compiler_flags:list&cflags&copts=[], linker_flags:list&ldflags&linkopts=[], pkg_config_libs:list=[], pkg_config_cflags:list=[],
              includes:list=[], defines:list|dict=[], alwayslink:bool=False, _c=False, visibility:list=None, deps:list=[], labels:list=[]):
    """Generate a C or C++ object file from a single source.

    N.B. This is fairly low-level; for most use cases cc_library should be preferred.

    Args:
      name (str): Name of the rule
      src (str): C or C++ source file to compile. This can be another rule, but if so it must
                 have exactly one output.
      hdrs (list): Header files. These will be made available to dependent rules, so the distinction
                   between srcs and hdrs is important.
      private_hdrs (list): Header files that are available only to this rule and not exported to
                           dependent rules.
      out (str): Name of the output file. Defaults to name + .o.
      optional_outs (list): Name of optional outputs.
      deps (list): Dependent rules.
      visibility (list): Visibility declaration for this rule.
      test_only (bool): If True, is only available to other test rules.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker; these will not be used here but will be
                           picked up by a cc_binary or cc_test rule.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      includes (list): List of include directories to be added to the compiler's path.
      defines (list | dict): List of tokens to define in the preprocessor.
                             Alternatively can be a dict of name -> value to define, in which case
                             values are surrounded by quotes.
      alwayslink (bool): If True, any binaries / tests using this library will link in all symbols,
                         even if they don't directly reference them. This is useful for e.g. having
                         static members that register themselves at construction time.
      labels (list): Labels to attach to this rule.
    """
    # Handle defines being passed as a dict, as a nicety for the user.
    if isinstance(defines, dict):
        defines = [k if v is None else f'{k}=\\"{v}\\"' for k, v in sorted(defines.items())]

    pkg = package_name()
    labels = (['cc:ld:' + flag for flag in linker_flags] +
              ['cc:pc:' + lib for lib in pkg_config_libs] +
              ['cc:pcc:' + cflag for cflag in pkg_config_cflags] +
              [f'cc:inc:{pkg}/{include}' for include in includes] +
              ['cc:def:' + define for define in defines] +
              labels)
    if alwayslink:
        labels += ['cc:al:{pkg}/{name}.a']
    cmds, tools = _library_cmds(_c, compiler_flags, pkg_config_libs, pkg_config_cflags, archive=False)

    return build_rule(
        name=name,
        srcs={'srcs': [src], 'hdrs': hdrs, 'priv': private_hdrs},
        outs=[out or name + '.o'],
        optional_outs=['*.gcno']+optional_outs,  # For coverage
        deps=deps,
        cmd=cmds,
        building_description='Compiling...',
        requires=['cc_hdrs', 'cc_mod'],
        test_only=test_only,
        labels=labels,
        tools=tools,
        pre_build=_library_transitive_labels(_c, compiler_flags, pkg_config_libs, pkg_config_cflags, archive=False)
                  if (deps or includes or defines) else None,
        needs_transitive_deps=True,
    )


def cc_static_library(name:str, srcs:list=[], hdrs:list=[], compiler_flags:list&cflags&copts=[], out:str='', optional_outs:list=[],
                      linker_flags:list&ldflags&linkopts=[], deps:list=[], visibility:list=None,
                      test_only:bool&testonly=False, pkg_config_libs:list=[], pkg_config_cflags:list=[],_c=False, labels:list=[]):
    """Generates a C++ static library (.a).

    This is essentially just a collection of other cc_library rules into a single archive.
    Optionally this rule can have sources of its own, but it's quite reasonable just to use
    it as a collection of other rules.

    Args:
      name (str): Name of the rule
      srcs (list): C or C++ source files to compile.
      hdrs (list): Header files.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker.
      deps (list): Dependent rules.
      out (str): Name of the output library. Defaults to lib<name>.a (or just <name>.a if name already
                       begins with 'lib').
      optional_outs (list): Name of optional outputs.
      visibility (list): Visibility declaration for this rule.
      test_only (bool): If True, is only available to other test rules.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`
      labels (list): Labels to attach to this rule.
    """
    provides = None
    if srcs or hdrs:
        lib_rule = cc_library(
            name = f'_{name}#lib' if srcs else f'_{name}#lib_hdrs',
            srcs = srcs,
            hdrs = hdrs,
            optional_outs = optional_outs,
            compiler_flags = compiler_flags,
            linker_flags = linker_flags,
            deps = deps,
            test_only = test_only,
            pkg_config_libs = pkg_config_libs,
            pkg_config_cflags = pkg_config_cflags,
            _c=_c,
        )
        deps += [lib_rule, f':_{name}#lib_hdrs'] if srcs else [lib_rule]
        provides = {
            'cc_hdrs': f':_{name}#lib_hdrs',
            'cc': ':' + name,
        }
    if not out:
        out = f'{name}.a' if name.startswith('lib') else f'lib{name}.a'
    return build_rule(
        name = name,
        deps = deps,
        outs = [out],
        cmd = '"$TOOLS_ARCAT" ar --find && "$TOOLS_AR" s "$OUT"',
        needs_transitive_deps = True,
        output_is_complete = True,
        visibility = visibility,
        test_only = test_only,
        building_description = 'Archiving...',
        provides = provides,
        requires = ['cc'],
        tools = {
            'arcat': [CONFIG.ARCAT_TOOL],
            'ar': [CONFIG.CC.AR_TOOL],
        },
        labels = labels,
    )


def cc_shared_object(name:str, srcs:list=[], hdrs:list=[], out:str='', optional_outs:list=[], compiler_flags:list&cflags&copts=[],
                     linker_flags:list&ldflags&linkopts=[], deps:list=[], visibility:list=None, test_only:bool&testonly=False,
                     pkg_config_libs:list=[], pkg_config_cflags:list=[], includes:list=[], _c=False, labels:list=[]):
    """Generates a C++ shared object (.so) with its dependencies linked in.

    Args:
      name (str): Name of the rule
      srcs (list): C or C++ source files to compile.
      hdrs (list): Header files. These will be made available to dependent rules, so the distinction
                   between srcs and hdrs is important.
      out (str): Name of the output .so. Defaults to lib<name>.so (or just <name>.so if name already begins with 'lib').
      optional_outs (list): Name of optional outputs.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker.
      deps (list): Dependent rules.
      visibility (list): Visibility declaration for this rule.
      test_only (bool): If True, is only available to other test rules.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`
      includes (list): Include directories to be added to the compiler's lookup path.
      labels (list): Labels to attach to this rule.
    """
    if CONFIG.CC.DEFAULT_LDFLAGS:
        linker_flags += CONFIG.CC.DEFAULT_LDFLAGS

    provides = None
    if srcs:
        lib_rule = cc_library(
            name = f'_{name}#lib',
            srcs = srcs,
            hdrs = hdrs,
            optional_outs = optional_outs,
            compiler_flags = compiler_flags,
            linker_flags = linker_flags,
            deps = deps,
            test_only = test_only,
            pkg_config_libs = pkg_config_libs,
            pkg_config_cflags = pkg_config_cflags,
            includes = includes,
            _c=_c,
        )
        deps += [lib_rule, f':_{name}#lib_hdrs']
        provides = {
            'cc_hdrs': f':_{name}#lib_hdrs',
            'cc': ':' + name,
        }
    cmds, tools = _binary_cmds(_c, linker_flags, pkg_config_libs, shared=True)
    if not out:
        out = f'{name}.so' if name.startswith('lib') else f'lib{name}.so'
    return build_rule(
        name=name,
        srcs={'srcs': srcs, 'hdrs': hdrs},
        outs=[out],
        deps=deps,
        visibility=visibility,
        cmd=cmds,
        building_description='Linking...',
        binary=True,
        needs_transitive_deps=True,
        output_is_complete=True,
        provides=provides,
        tools=tools,
        test_only=test_only,
        requires=['cc', 'cc_hdrs'],
        pre_build=_binary_transitive_labels(_c, linker_flags, pkg_config_libs, shared=True) if deps else None,
        labels=labels,
    )


def cc_module(name:str, srcs:list=[], hdrs:list=[], interfaces:list=[], private_hdrs:list=[],
              deps:list=[], visibility:list=None, test_only:bool&testonly=False,
              compiler_flags:list&cflags&copts=[], linker_flags:list&ldflags&linkopts=[],
              pkg_config_libs:list=[], pkg_config_cflags:list=[], includes:list=[],
              defines:list|dict=[], alwayslink:bool=False, labels:list=[], optional_outs:list=[]):
    """Generate a C++ module.

    This is still experimental. Currently it has only been tested with clang; support for GCC
    will be added later once versions of GCC supporting modules are more conveniently available.

    Args:
      name (str): Name of the rule
      srcs (list): C++ source files to compile.
      hdrs (list): Header files. These will be made available to dependent rules, so the distinction
                   between srcs and hdrs is important.
      interfaces (list): Module interface files. Again, these are treated differently to `srcs` in
                         terms of compilation so the distinction is important.
      private_hdrs (list): Header files that are available only to this rule and not exported to
                           dependent rules.
      deps (list): Dependent rules.
      visibility (list): Visibility declaration for this rule.
      test_only (bool): If True, is only available to other test rules.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker; these will not be used here but will be
                           picked up by a cc_binary or cc_test rule.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`. Again, the ldflags
                              will be picked up by cc_binary or cc_test rules.
      includes (list): List of include directories to be added to the compiler's path.
      defines (list | dict): List of tokens to define in the preprocessor.
                             Alternatively can be a dict of name -> value to define, in which case
                             values are surrounded by quotes.
      alwayslink (bool): If True, any binaries / tests using this library will link in all symbols,
                         even if they don't directly reference them. This is useful for e.g. having
                         static members that register themselves at construction time.
      linkstatic (bool): Only provided for Bazel compatibility. Has no actual effect.
      textual_hdrs (list): Also provided for Bazel compatibility. Effectively works the same as hdrs for now.
      labels (list): Labels to attach to this rule.
      optional_outs (list): Name of optional outputs.
    """
    return cc_library(
        name = name,
        srcs = srcs,
        hdrs = hdrs,
        _interfaces = interfaces,
        private_hdrs = private_hdrs,
        optional_outs = optional_outs,
        deps = deps,
        visibility = visibility,
        test_only = test_only,
        compiler_flags = compiler_flags,
        linker_flags = linker_flags,
        pkg_config_libs = pkg_config_libs,
        pkg_config_cflags = pkg_config_cflags,
        includes = includes,
        defines = defines,
        alwayslink = alwayslink,
        _module = True,
        labels = labels,
    )


def cc_binary(name:str, srcs:list=[], hdrs:list=[], private_hdrs:list=[],
              compiler_flags:list&cflags&copts=[], linker_flags:list&ldflags&linkopts=[],
              deps:list=[], visibility:list=None, pkg_config_libs:list=[], includes:list=[], defines:list|dict=[],
              pkg_config_cflags:list=[], test_only:bool&testonly=False, static:bool=False, _c=False,
              linkstatic:bool=False, labels:list=[], optional_outs:list=[]):
    """Builds a binary from a collection of C++ rules.

    Args:
      name (str): Name of the rule
      srcs (list): C or C++ source files to compile.
      hdrs (list): Header files.
      private_hdrs (list): Header files that are available only to this rule and not exported to
                           dependent rules.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker.
      deps (list): Dependent rules.
      visibility (list): Visibility declaration for this rule.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`
      includes (list): List of include directories to be added to the compiler's path.
      defines (list | dict): List of tokens to define in the preprocessor.
                             Alternatively can be a dict of name -> value to define, in which case
                             values are surrounded by quotes.
      test_only (bool): If True, this rule can only be used by tests.
      static (bool): If True, the binary will be linked statically.
      linkstatic (bool): Only provided for Bazel compatibility. Has no actual effect since we always
                         link roughly equivalently to their "mostly-static" mode.
      labels (list): Labels to attach to this rule.
      optional_outs (list): Name of optional outputs.
    """
    if CONFIG.BAZEL_COMPATIBILITY:
        linker_flags = ['-lpthread' if l == '-pthread' else l for l in linker_flags]
    if CONFIG.CC.DEFAULT_LDFLAGS:
        linker_flags += CONFIG.CC.DEFAULT_LDFLAGS
    if static:
        linker_flags += ['-static']
    cmds, tools = _binary_cmds(_c, linker_flags, pkg_config_libs, static=static)
    if srcs:
        if static:
            compiler_flags += ["-static", """'{{ gcc ? "-static-libgcc" }}'"""]
        lib_rule = cc_library(
            name=f'_{name}#lib',
            srcs=srcs,
            hdrs=hdrs,
            private_hdrs=private_hdrs,
            optional_outs = optional_outs,
            deps=deps,
            pkg_config_libs=pkg_config_libs,
            pkg_config_cflags=pkg_config_cflags,
            includes=includes,
            defines=defines,
            compiler_flags=compiler_flags,
            test_only=test_only,
            _c=_c,
        )
        deps += [lib_rule]
    return build_rule(
        name=name,
        outs=[name],
        deps=deps,
        visibility=visibility,
        cmd=cmds,
        building_description='Linking...',
        binary=True,
        needs_transitive_deps=True,
        output_is_complete=True,
        requires=['cc'],
        tools=tools,
        pre_build=_binary_transitive_labels(_c, linker_flags, pkg_config_libs),
        test_only=test_only,
        optional_outs = [f"{name}.dSYM"] if CONFIG.CC.DSYM_TOOL else [],
        labels=labels,
    )


def cc_test(name:str, srcs:list=[], hdrs:list=[], compiler_flags:list&cflags&copts=[],
            linker_flags:list&ldflags&linkopts=[], pkg_config_libs:list=[],
            pkg_config_cflags:list=[], deps:list=[], worker:str='', data:list|dict=[],
            visibility:list=[], flags:str='', labels:list&features&tags=[], flaky:bool|int=0,
            test_outputs:list=[], size:str=None, timeout:int=0,
            sandbox:bool=None, write_main:bool=False, linkstatic:bool=False, _c=False):
    """Defines a C++ test.

    We template in a main file so you don't have to supply your own.
    (Later we might allow that to be configured to help support other unit test frameworks).

    Args:
      name (str): Name of the rule
      srcs (list): C or C++ source files to compile.
      hdrs (list): Header files.
      compiler_flags (list): Flags to pass to the compiler.
      linker_flags (list): Flags to pass to the linker.
      pkg_config_libs (list): Libraries to declare a dependency on using `pkg-config --libs`
      pkg_config_cflags (list): Libraries to declare a dependency on using `pkg-config --cflags`
      deps (list): Dependent rules.
      worker (str): Reference to worker script, A persistent worker process that is used to set up the test.
      data (list): Runtime data files for this test.
      visibility (list): Visibility declaration for this rule.
      flags (str): Flags to apply to the test invocation.
      labels (list): Labels to attach to this test.
      flaky (bool | int): If true the test will be marked as flaky and automatically retried.
      test_outputs (list): Extra test output files to generate from this test.
      size (str): Test size (enormous, large, medium or small).
      timeout (int): Length of time in seconds to allow the test to run for before killing it.
      sandbox (bool): Sandbox the test on Linux to restrict access to namespaces such as network.
      write_main (bool): Deprecated, has no effect. See `plz help testmain` for more information
                         about how to define a default dependency for the test main.
      linkstatic (bool): Only provided for Bazel compatibility. Has no actual effect since we always
                         link roughly equivalently to their "mostly-static" mode.
    """

    if CONFIG.BAZEL_COMPATIBILITY:
        linker_flags = ['-lpthread' if l == '-pthread' else l for l in linker_flags]
    if CONFIG.CC.DEFAULT_LDFLAGS:
        linker_flags += CONFIG.CC.DEFAULT_LDFLAGS
    if CONFIG.CC.TEST_MAIN and not _c:
        deps += [CONFIG.CC.TEST_MAIN]
    cmds, tools = _binary_cmds(_c, linker_flags, pkg_config_libs)

    if srcs:
        lib_rule = cc_library(
            name=f'_{name}#lib',
            srcs=srcs,
            hdrs=hdrs,
            deps=deps,
            pkg_config_libs=pkg_config_libs,
            pkg_config_cflags=pkg_config_cflags,
            compiler_flags=compiler_flags,
            test_only=True,
            alwayslink=True,
            _c=_c,
        )
        deps += [lib_rule]

    test_cmd = f'$TEST {flags}'
    test_tools = {}
    if worker:
        test_cmd = f'$(worker {worker}) && {test_cmd} '
        deps += [worker]
    if CONFIG.CC.COVERAGE:
        test_cmd = {
            'opt': test_cmd,
            'dbg': test_cmd,
            'cover': test_cmd + '; R=$?; cp $GCNO_DIR/*.gcno . && $TOOLS_COVERAGE *.gcda && cat *.gcov > test.coverage; exit $R'
        }
        test_tools['coverage'] = CONFIG.CC.COVERAGE_TOOL

    return build_rule(
        name=name,
        outs=[name],
        deps=deps,
        data=data,
        visibility=visibility,
        cmd=cmds,
        test_cmd=test_cmd,
        building_description='Linking...',
        binary=True,
        test=True,
        needs_transitive_deps=True,
        output_is_complete=True,
        requires=['cc', 'cc_hdrs', 'test'],
        labels=labels,
        tools=tools,
        test_tools = test_tools,
        pre_build=_binary_transitive_labels(_c, linker_flags, pkg_config_libs),
        flaky=flaky,
        test_outputs=test_outputs,
        test_timeout=timeout,
        size = size,
        test_sandbox=sandbox,
    )


def _default_cflags(c:bool=False, dbg:bool=False):
    """Returns the default list of cflags / cppflags for opt/dbg as appropriate."""
    if c:
        return CONFIG.CC.DEFAULT_DBG_CFLAGS if dbg else CONFIG.CC.DEFAULT_OPT_CFLAGS
    else:
        return CONFIG.CC.DEFAULT_DBG_CPPFLAGS if dbg else CONFIG.CC.DEFAULT_OPT_CPPFLAGS


def _build_flags(compiler_flags:list=[], pkg_config_libs:list=[], pkg_config_cflags:list=[], defines:list=[], c:bool=False, dbg:bool=False):
    """Builds the list of flags that we'll pass to the compiler invocation."""
    cflags = _default_cflags(c, dbg) + ["-fPIC"]

    if CONFIG.CC.ENABLE_GC and not dbg:
        cflags += [
            # Place all functions and data (global variables, constants) into their own sections in the object code.
            # These options are only meaningful when building ELF object code, but they're both treated as no-ops by all
            # the compilers we support when building Mach-O object code, so (thankfully) there's no need to figure out
            # whether to pass them based on the target architecture (which we don't necessarily know here).
            "-fdata-sections",
            "-ffunction-sections",
            # -ffunction-sections causes sections to be emitted with unique names. Avoid doing this, if the compiler
            # and linker both support it: it significantly increases the size of the ELF string table. This requires a
            # linker that can handle repeated section names, which all reasonably modern versions of linkers that
            # generate ELF code already do (e.g. GNU ld/gold >= 2.35).
            """'{{ clang || aclang ? "-fno-unique-section-names" }}'""",
        ]

    # Now append the compiler flags we were given, which allows the plugin defaults (and whatever else we set above) to
    # be overridden (i.e. by the user).
    cflags += compiler_flags
    if defines:
        cflags += ['-D' + define for define in defines]

    pkg_config_cmds = [f"`pkg-config --cflags {x}`" for x in (pkg_config_cflags + pkg_config_libs)]

    return cflags + pkg_config_cmds


def _binary_build_flags(linker_flags:list=[], pkg_config_libs:list=[], shared:bool=False, alwayslink:list=[], c:bool=False, dbg:bool=False,
                        static:bool=False):
    """Builds the list of flags that we'll pass to the linker invocation."""
    pkg_config_cmds = [f"`pkg-config --libs {x}`" for x in pkg_config_libs]

    # Construct oflags, a list of linker flags (passed to it via the compiler) that either dynamically or statically
    # link the object files and static libraries (*.[oa]) discovered in the sandbox into an executable.
    #
    # There are three complications, two of them caused by the need to be compatible with a broad range of operating
    # systems and linkers:
    #
    # - We can't determine ahead of time the paths to the object files and static libraries we need to link, so they
    #   need to be discovered by the shell command when the build action runs, hence the find(1) invocations. The output
    #   of find(1) is sorted to improve determinism.
    # - When creating a shared library, every object file in the static libraries must be included in the link; this is
    #   only the case for the static libraries given in alwayslink when creating a static library. This is indicated
    #   differently for different linkers: the non-Darwin linkers use the --whole-archive flag (which is boolean and
    #   remains in effect until the next occurrence of the --no-whole-archive flag), whereas the Darwin linkers use the
    #   -force_load flag (which accepts a string value and must be repeated for each library). We therefore have to
    #   construct both the "--whole-archive a.a b.a c.a ... --no-whole-archive" and "-force_load a.a -force_load b.a
    #   -force_load c.a ..." list, and make sure the appropriate list is passed to the linker depending on its identity.
    #   We have to rely on tr(1) and sed(1) to manipulate find(1)'s output into please_cc's syntax because BSD's find(1)
    #   doesn't support the -printf option. Neither class of linker applies the effect of --whole-archive or -force_load
    #   to object files, so we don't waste time partitioning object files and static libraries when constructing either
    #   list.
    # - Linkers expect the list of libraries to be passed in evaluation order (i.e., libraries earlier in the list must
    #   not depend on libraries later in the list). Non-Darwin linkers support the --start-group and --end-group flags,
    #   which allow libraries to be passed in an arbitrary order; this is convenient for us, given that we're relying
    #   on find(1) to locate the static libraries we need to link and can't easily derive their evaluation order here.
    #   (In many cases, linkers that don't support these flags will still successfully link the binary even when
    #   libraries aren't passed in evaluation order, so Apple's linkers not supporting these flags doesn't necessarily
    #   preclude us from being able to link Darwin binaries.
    #
    # The structure of oflags, therefore, broadly looks like this:
    #
    #   if shared:
    #     -shared {-Wl,--start-group} {-Wl,--whole-archive} [*.[oa]] {-Wl,--no-whole-archive} {-Wl,--end-group}
    #   else:
    #     {-Wl,--start-group} {-Wl,--whole-archive} [alwayslink] {-Wl,--no-whole-archive} [*.[oa]] {-Wl,--end-group}
    oflags = ["-shared"] if shared else []
    oflags += ["""'{{ !ld64 && !appleld ? ["-Wl,--start-group", "-Wl,--whole-archive"] }}'"""]
    if shared:
        oflags += ['''"{{ ld64 || appleld ? [`find . -name '*.o' -or -name '*.a' | sort | sed -e 's/\(.*\)/"-Wl,-force_load","\1"/' | tr '\n' , | sed -e 's/.$//'`] : [`find . -name '*.o' -or -name '*.a' | sort | sed -e 's/\(.*\)/"\1"/' | tr '\n' , | sed -e 's/.$//'`] }}"''']
    elif alwayslink:
        oflags += ["""'{{ ld64 || appleld ? ["-Wl,-force_load", "%s"] : ["%s"] }}'""" % (al, al) for al in alwayslink]
    oflags += ["""'{{ !ld64 && !appleld ? ["-Wl,--no-whole-archive"] }}'"""]
    if not shared:
        oflags += ["`find . -name '*.o' -or -name '*.a' | sort`"]
    oflags += ["""'{{ !ld64 && !appleld ? "-Wl,--end-group" }}'"""]

    # Don't add a .note.gnu.build-id section to ELF images. This improves the binary's determinism.
    lflags = ["""'{{ gnuld || gold || lld ? "-Wl,--build-id=none" }}'"""]

    if CONFIG.CC.ENABLE_GC and not dbg:
        lflags += ["""'{{ ld64 || appleld ? "-Wl,-dead_strip" : "-Wl,--gc-sections" }}'"""]

    lflags += [_escape_linker_flag(f) for f in linker_flags]

    lflags += _default_cflags(c, dbg)

    if static:
        lflags += ["-static"]

    return oflags + lflags + pkg_config_cmds


def _escape_linker_flag(flag:str):
    """Returns a linker flag suitable for passing to a C/C++ compiler toolchain - the flag is prepended with "-Wl," and
    spaces are replaced with commas.

    Alternatively, please_cc expressions are permitted, but cannot be escaped automatically because of their non-trivial
    structure; the escaping must therefore be performed as part of the expression itself.
    """
    if flag.startswith("{{ ") and flag.endswith(" }}"):
        return f"'{flag}'" if '"' in flag else f'"{flag}"'
    else:
        return "-Wl," + flag.replace(" ", ",")


def _library_cmds(c:bool=False, compiler_flags:list=[], pkg_config_libs:list=[], pkg_config_cflags:list=[], extra_flags:list=[],
                  archive:bool=True):
    """Returns the commands needed for a cc_library rule."""
    # Most compilers allow multiple action flags to be specified, with later flags overriding earlier ones. However,
    # specifying more than one action is an error (-Wunused-command-line-argument) as of Clang 20, so if the compiler
    # flags we were given contain one, we need to avoid adding our default action when building libraries (-c, a.k.a.
    # "only run preprocess, compile and assemble steps").
    common_flags = [] if any([cf in _ACTION_FLAGS for cf in compiler_flags]) else ["-c"]

    common_flags += ["-I", "."]
    dbg_flags = _build_flags(compiler_flags, pkg_config_libs, pkg_config_cflags, c=c, dbg=True)
    opt_flags = _build_flags(compiler_flags, pkg_config_libs, pkg_config_cflags, c=c)
    cmd_template = '"$TOOLS_PLEASE_CC" cc "$TOOLS_CC" %s ${SRCS_SRCS}'
    if archive:
        cmd_template += ' && "$TOOLS_ARCAT" ar -r && "$TOOLS_AR" s "$OUT"'
    cmds = {
        'dbg': cmd_template % ' '.join(common_flags + dbg_flags + extra_flags),
        'opt': cmd_template % ' '.join(common_flags + opt_flags + extra_flags),
    }
    if CONFIG.CC.COVERAGE:
        cmds['cover'] = cmd_template % ' '.join(common_flags + dbg_flags + _COVERAGE_FLAGS + extra_flags)
    return cmds, {
        'please_cc': CONFIG.CC.PLEASE_CC_TOOL,
        'cc': [CONFIG.CC.CC_TOOL if c else CONFIG.CC.CPP_TOOL],
        'arcat': [CONFIG.ARCAT_TOOL if archive else None],
        'ar': [CONFIG.CC.AR_TOOL if archive else None],
    }


def _binary_cmds(c:bool=False, linker_flags:list=[], pkg_config_libs:list=[], extra_flags:list=[], shared:bool=False, alwayslink:list=[],
                 static:bool=False):
    """Returns the commands needed for a cc_binary, cc_test or cc_shared_object rule."""
    dbg_flags = _binary_build_flags(linker_flags, pkg_config_libs, shared, alwayslink, c=c, dbg=True, static=static)
    opt_flags = _binary_build_flags(linker_flags, pkg_config_libs, shared, alwayslink, c=c, dbg=False, static=static)
    cmd_template = '"$TOOLS_PLEASE_CC" cc "$TOOLS_CC" -o "$OUT" %s'
    cmds = {
        'dbg': cmd_template % ' '.join(dbg_flags + extra_flags),
        'opt': cmd_template % ' '.join(opt_flags + extra_flags),
    }
    tools = {
        'please_cc': CONFIG.CC.PLEASE_CC_TOOL,
        'cc': (CONFIG.CC.CC_TOOL if c else CONFIG.CC.CPP_TOOL),
    }
    if CONFIG.CC.COVERAGE:
        cmds['cover'] = cmd_template % ' '.join(dbg_flags + extra_flags + _COVERAGE_FLAGS)

    if CONFIG.CC.DSYM_TOOL:
        dbg = cmds['dbg']
        cmds['dbg'] = f'{dbg} && {CONFIG.CC.DSYM_TOOL} $OUT'
    return cmds, tools


def _module_file_flag(module_file:str):
    """Generates an appropriate C++ compiler flag for specifying the path to a precompiled module file.

    From Clang 17 (and Apple Clang 16) onwards, not specifying the module's name along with its path generates a warning
    (-Weager-load-cxx-named-modules), but specifying the module's name at all is a syntax error prior to Clang 16 (or Apple
    Clang 15). When using Clang >= 16, specify the basename of the module file as the module name; nothing in the C++
    specification says that a single module has to be declared in a file of the same name, but it'll be the case most of the
    time (and will fail loudly in the other cases).
    """
    _, file = split_path(module_file)
    name, _ = splitext(file)
    return """'{{ (clang && clang < 16) || (aclang && aclang < 15) ? "-fmodule-file=%s" : "-fmodule-file=%s=%s" }}'""" % (
        module_file,
        name,
        module_file,
    )


def _library_transitive_labels(c:bool=False, compiler_flags:list=[], pkg_config_libs:list=[], pkg_config_cflags:list=[], archive:bool=True):
    """Applies commands from transitive labels to a cc_library rule."""
    def apply_transitive_labels(name):
        labels = get_labels(name, 'cc:')
        flags = []
        for l in labels:
            if l.startswith("inc:"):
                flags += ["-isystem", l[4:]]
            elif l.startswith("def:"):
                flags += ["-D", l[4:]]

        pkg_config_libs += [l[3:] for l in labels if l.startswith('pc:') and l[3:] not in pkg_config_libs]
        pkg_config_cflags += [l[4:] for l in labels if l.startswith('pcc:') and l[4:] not in pkg_config_cflags]
        mods = [_module_file_flag(l[4:]) for l in labels if l.startswith('mod:')]
        flags += mods
        if mods:
            flags += _MODULE_FLAGS
        if flags:  # Don't update if there aren't any relevant labels
            cmds, _ = _library_cmds(c, compiler_flags, pkg_config_libs, pkg_config_cflags, flags, archive=archive)
            for k, v in cmds.items():
                set_command(name, k, v)
    return apply_transitive_labels


def _binary_transitive_labels(c:bool=False, linker_flags:list=[], pkg_config_libs:list=[], shared:bool=False):
    """Applies commands from transitive labels to a cc_binary, cc_test or cc_shared_object rule."""
    def apply_transitive_labels(name):
        labels = get_labels(name, 'cc:')
        flags = ['-Wl,' + l[3:].replace(" ", ",") for l in labels if l.startswith('ld:')]

        flags += ['`pkg-config --libs %s`' % l[3:] for l in labels if l.startswith('pc:')]

        # ./ here because some weak linkers don't realise ./lib.a is the same file as lib.a
        # and report duplicate symbol errors as a result.
        alwayslink = ["./" + l[3:] for l in labels if l.startswith('al:')]
        # Probably a little optimistic to check this (most binaries are likely to have *some*
        # kind of linker flags to apply), but we might as well.
        if flags or alwayslink:
            cmds, _ = _binary_cmds(c, linker_flags, pkg_config_libs, flags, shared, alwayslink)
            for k, v in cmds.items():
                set_command(name, k, v)
    return apply_transitive_labels


if CONFIG.BAZEL_COMPATIBILITY:
    # For nominal Buck compatibility. The cc_ forms are preferred.
    cxx_binary = cc_binary
    cxx_library = cc_library
    cxx_test = cc_test
